#!/usr/bin/perl

use strict;

$/ = '<entryFree';

while (<>) {
    s#^\s*key=[^>]+>\s*##s;
    s#<entryFree$##;

    while (s/^<orth[^>]*>([^<]+)<\/orth>[,\s]*<pos>s\.<\/pos>\s*<gen>([mf])\.<\/gen>/ /s) {
	my $lem = $1;
	my $gen = $2;

	donom($lem,$gen);
    }

    while (s/<gen>([mf])\.<\/gen>\s+<orth[^>]*>([^<]+)<\/orth>/ /s) {
	my $gen = $1;
	my $lem = $2;

	donom($lem,$gen);
    }

    while (s/<orth[^>]*>([^<]+)<\/orth>[,\s]*(?:<pos>s\.<\/pos>\s*)?<gen>([mf])\.<\/gen>/ /s) {
	my $lem = $1;
	my $gen = $2;

	donom($lem,$gen);
    }

    while (s/<orth[^>]*>([^<]+)<\/orth>[,\s]*<pos>adj\.<\/pos>/ /s
	   or s/<pos>adj\.<\/pos>\s*<orth[^>]*>([^<]+)<\/orth>/ /s) {
	my $lem = $1;

	next unless $lem = fixlem($lem);

	my $stem = $lem;
	my $class = '';

	if ($lem =~ /nta$/)
	{
	    print "\n:le:$lem\n";
	    print ":wd:$lem\tnumeral\n";
	    next;
	}

	$stem =~ s/a$/o/;	# some adj only appear in the feminine in this dictionary
	
	if ($stem =~ s/co$//) {
	    $class = 'co_ca';
	}
	elsif ($stem =~ s/go$//) {
	    $class = 'go_ga';
	}
	elsif ($stem =~ s/[o]$//) {
	    $class = 'o_a';
	}
	elsif ($stem =~ s/[e]$//) {
	    $class = 'e_i';
	}

	next if $class eq '';

	print "\n:le:$lem\n";
	print ":aj:$stem\t$class\n";
    }

    while (s/<orth[^>]*>([^<]+)<\/orth>[,\s]*<pos>adv\.<\/pos>/ /s
	   or s/<pos>adv\.<\/pos>\s*<orth[^>]*>([^<]+)<\/orth>/ /s) {
	my $lem = $1;

	next unless $lem = fixlem($lem);

	print "\n:le:$lem\n";
	print ":wd:$lem\tadverb\n";
    }

    while (s/<orth[^>]*>([^<]+)<\/orth>[,\s]*<pos>v\.<\/pos>/ /s) {
	my $lem = $1;

	doverb($lem);
    }
}

sub donom {
    my($lem,$gen) = @_;

    $lem = fixlem($lem);

    return unless defined($lem);

    $gen = $gen eq 'f' ? 'fem' : 'masc';
    
    my $class = 'indecl';
    my $stem = $lem;

    if ($stem =~ s/io$//) {
	$class = 'io_i';
    }
    elsif ($stem =~ s/o$//) {
	$class = 'o_i';
    }
    elsif ($stem =~ s/a$//) {
	$class = 'a_e';
    }
    elsif ($stem =~ s/e$//) {
	$class = 'e_i';
    }
    elsif ($stem =~ s/a\\$//) {
	$class = 'a_acc';
    }
    elsif ($stem =~ s/u\\$//) {
	$class = 'u_acc';
    }

    print "\n:le:$lem\n";
    print ":no:$stem\t$class $gen\n";
}

sub doverb {
    my($lem) = @_;

    $lem = fixlem($lem);

    return unless defined($lem);

    my $class = '';
    my $stem = $lem;

    if ($stem =~ s/(care|gare|ciare|giare|are|ere|ire|urre)$//) {
	$class = $1 . "_vb";
    }
    else
    {
	$class = "irreg_vb";
    }

    return if $class eq '';
    
    print "\n:le:$lem\n";
    print ":de:$stem $class\n";
}

sub fixlem {
    my($lem) = @_;

    return undef if ($lem =~ /\s/);

    $lem =~ tr/\-//d;
    $lem =~ s/\&([A-Za-z])(uml|acute);/$1/g;
    $lem =~ s/\&([A-Za-z])grave;/$1\\/g;

    lc($lem);
}
